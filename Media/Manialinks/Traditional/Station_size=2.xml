<?xml version="1.0" encoding="utf-8" standalone="yes" ?>
<manialink version="1" background="stars">
	<timeout>0</timeout>
	<frame id="frmStart" posn="0 -34">
		<quad halign="center" valign="top" sizen="100 30" image="file://Media/Manialinks/Common/StationInfo/StdButtonOff.dds" imagefocus="file://Media/Manialinks/Common/StationInfo/StdButtonOn.dds" id="quadStart" ScriptEvents="1" />
		<label textcolor="9ef" style="TextButtonMedium" halign="center" id="lblStart" valign="center" posn="0 -14" textsize="10">Loading...</label>
	</frame>
	<script><!--
		#Include "TextLib" as TextLib
		
		declare Version = -1;
		declare MustUpdate = False;
		
		//NEWSTICKER
		declare MaxLength = 69;
		declare News = "";
		declare LastAnimTick = Now;
		declare DoAnim = False;
		declare Pos = 0;
		declare CurText = "";
		declare Timeout = 3000;
		
		declare Req = Http.CreateGet("http://vc.mewin.de/traditional/get.php?get=version");
		
		declare ReqStart = Now;
		while (Now < ReqStart + Timeout)
		{
			if (Req.IsCompleted)
			{
				if (Req.StatusCode == 200)
				{
					declare Doc = Xml.Create(Req.Result);
					
					Version = Doc.GetFirstChild("data").GetAttributeInteger("value", -1);
				}
				break;
			}
			yield;
		}
		
		Http.Destroy(Req);
		
		Req = Http.CreateGet("http://vc.mewin.de/traditional/get.php?get=news");
		
		ReqStart = Now;
		while (Now < ReqStart + Timeout)
		{
			if (Req.IsCompleted)
			{
				if (Req.StatusCode == 200)
				{
					declare Doc = Xml.Create(Req.Result);
					
					News = Doc.GetFirstChild("data").GetAttributeText("value", "");
				}
				break;
			}
			yield;
		}
		
		Http.Destroy(Req);
	--></script>
	<include url="file://Media/Manialinks/Traditional/Version.xml" />
	<label scale="2" id="News" haling="left" valign="bottom" posn="-160 -60 2" style="StyleTextScriptEditor" text="" />
	<script><!--
		declare StartLabel = (Page.GetFirstChild("lblStart") as CMlLabel);
		if (MustUpdate)
		{
			
			StartLabel.Value ="Update";
		}
		else
		{
			StartLabel.Value = "Start";
		}
		
		declare NewsLbl = (Page.GetFirstChild("News") as CMlLabel);
		if (TextLib::Length(News) <= MaxLength)
		{
			NewsLbl.SetText("$000" ^ News);
		}
		else
		{
			News = News ^ " ... ";
			DoAnim = True;
			CurText = News;
		}
		while (True)
		{
			yield;
			if (LastAnimTick + 300 < Now)
			{
				LastAnimTick = Now;
				Pos += 1;
				if (Pos > TextLib::Length(News))
				{
					Pos = 0;
				}
				CurText = TextLib::SubString(News, Pos, TextLib::Length(News) - Pos) ^  TextLib::SubString(News, 0, Pos);
				NewsLbl.SetText("$fff$s" ^ TextLib::SubString(CurText, 0, MaxLength));
			}
			foreach (Event in PendingEvents)
			{
				if (Event.Type == CMlEvent::Type::MouseClick && Event.ControlId == "quadStart")
				{
					if (MustUpdate)
					{
						OpenLink("install_traditional", CMlScript::LinkType::ManialinkBrowser);
					}
					else
					{
						EnterStation();
					}
				}
			}
		}
	--></script>
</manialink>