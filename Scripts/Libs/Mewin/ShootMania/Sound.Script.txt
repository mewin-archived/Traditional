declare Text[] PrecachedSounds;

Void InitForPlayer(CSmPlayer Player)
{
    declare CUILayer LayerSound for Player;
    
    LayerSound <=> Null;
}

Void PlaySound(CSmPlayer Player, Text Name)
{
    if (!Player.IsFakePlayer)
    {
        declare CUILayer LayerSound for Player;
        
        LayerSound.ManialinkPage = """<audio looping="0" play="1" hidden="1" data="file://Media/Sounds/{{{Name}}}.ogg" />{{{Now}}}""";
    }
}

Void PlaySoundAll(Text Name)
{
    foreach(Player in Players)
    {
        PlaySound(Player, Name);
    }

	foreach (Player in Spectators)
	{
		PlaySound(Player, Name);
	}
}

Void PlaySoundClan(Text Name, Integer Clan)
{
	foreach (Player in Players)
	{
		if (Player.CurrentClan == Clan)
		{
			PlaySound(Player, Name);
		}
	}
}

Void PreCacheSoundForPlayer(CSmPlayer Player, Text Name)
{
	if (!Player.IsFakePlayer)
    {
        declare CUILayer LayerSound for Player;
        
        LayerSound.ManialinkPage = """<audio looping="0" play="0" hidden="1" data="file://Media/Sounds/{{{Name}}}.ogg" />{{{Now}}}""";
    }
}

Void PreCacheSound(Text Name)
{
	PrecachedSounds.add(Name);
}

Void PreCacheSoundsForPlayer(CSmPlayer Player)
{
	foreach (Sound in PrecachedSounds)
	{
		PreCacheSoundForPlayer(Player, Sound);
	}
}

Void AddUILayer(CSmPlayer Player)
{
	declare CUILayer LayerSound for Player;
	declare LayerSoundId for Player = NullId;
	
	if (LayerSoundId == NullId || !UIManager.GetUI(Player).UILayers.existskey(LayerSoundId))
	{
		LayerSound <=> UIManager.UILayerCreate();
		LayerSoundId = LayerSound.Id;
		PreCacheSoundsForPlayer(Player);
	}
	else
	{
		LayerSound = UIManager.GetUI(Player).UILayers[LayerSoundId];
	}
	
	UIManager.GetUI(Player).UILayers.add(LayerSound);
}